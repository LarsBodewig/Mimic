buildscript {
    group = "$groupId"
}

plugins {
    id 'eclipse'
    id 'java-gradle-plugin'
    id 'maven-publish'
}

repositories {
    mavenLocal()
    mavenCentral()
    gradlePluginPortal()
    maven { url 'https://repo.gradle.org/gradle/libs-releases' }
}

java {
    sourceCompatibility = JavaVersion.VERSION_19
    withJavadocJar()
    withSourcesJar()
}

gradlePlugin {
    plugins {
        mimic {
            id = 'dev.bodewig.mimic'
            displayName = 'Mimic Gradle Plugin'
            description = 'Create reflective accessors for third party classes'
            tags.set(['reflection', 'codegen', 'java', 'wrapper'])
            implementationClass = 'dev.bodewig.mimic.gradle.plugin.MimicPlugin'
            website = 'https://github.com/LarsBodewig/Mimic'
            vcsUrl = 'https://github.com/LarsBodewig/Mimic.git'
        }
    }
}

publishing.publications.withType(MavenPublication) { pub ->
    pom.withXml {
        def pom = asNode()
        def parent = pom.appendNode('parent')
        parent.appendNode('groupId', "$groupId")
        parent.appendNode('artifactId', 'mimic-parent')
        parent.appendNode('version', "$version")
        pom.remove(pom.get('groupId'))
        pom.remove(pom.get('version'))
    }
}

dependencies {
    implementation "dev.bodewig.mimic:mimic-core:$version"
    implementation 'org.gradle:gradle-tooling-api:7.4.2'
    runtimeOnly 'org.slf4j:slf4j-simple:1.7.10'
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

sourceSets.main.java.srcDirs = [ "src/" ]

eclipse.project.name = "$name"
